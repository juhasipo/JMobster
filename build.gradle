subprojects {
    apply plugin: 'java'
    group = 'fi.vincit.jmobster'
    version = '0.3.1-alpha'
    archivesBaseName = 'jmobster-' + archivesBaseName
    sourceCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    jar {
        manifest {
            attributes 'Implementation-Title': archivesBaseName,
                    'Implementation-Version': version
        }
    }

    dependencies {
        // Needed for JSR-303 annotations
        compile group: 'org.hibernate', name: 'hibernate-validator', version: '4.3.0.Final'
        runtime group: 'org.hibernate', name: 'hibernate-validator-annotation-processor', version: '4.3.0.Final'

        compile group: 'org.slf4j', name: 'slf4j-api', version: '1.6.5'
        // Only for logging in test programs
        runtime group: 'ch.qos.logback', name: 'logback-classic', version: '1.0.6'
        runtime group: 'ch.qos.logback', name: 'logback-core', version: '1.0.6'

        // Test dependencies
        testCompile group: 'junit', name: 'junit', version: '4.10'
        testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.0'
    }
}

project(':core') {
    configurations {
        testutil
    }

    task testutilJar(type: Jar) {
        baseName = 'testutil'
        dependsOn classes
        from sourceSets.test.output
        include('fi/vincit/jmobster/**')
    }
    artifacts {
        testutil testutilJar
    }
}

// Validators

project(':validators:hibernate') {
    dependencies {
        compile project(':core')
        testCompile project(path: ':core', configuration: 'testutil')
    }
}
project(':validators:jsr303') {
    dependencies {
        compile project(':core')
        testCompile project(path: ':core', configuration: 'testutil')
    }
}


// Languages

project(':languages:javascript') {
    dependencies {
        compile project(':core')
        testCompile project(path: ':core', configuration: 'testutil')
    }
}



// Frameworks

project(':frameworks:backbone:backbone-core') {
    dependencies {
        compile project(':core')
        compile project(':languages:javascript')
        testCompile project(path: ':core', configuration: 'testutil')
        testCompile project(':validators:jsr303')
        testCompile project(':validators:hibernate')
    }
}
project(':frameworks:backbone:backbone-hibernate') {
    dependencies {
        compile project(':frameworks:backbone:backbone-core')
        compile project(':validators:hibernate')
        testCompile project(path: ':core', configuration: 'testutil')
    }
}
project(':frameworks:backbone:backbone-jsr303') {
    dependencies {
        compile project(':frameworks:backbone:backbone-core')
        compile project(':validators:jsr303')
        testCompile project(path: ':core', configuration: 'testutil')
    }
}


// Testing

project(':testing') {
    jar {
        manifest {
            attributes 'Implementation-Title': 'JMobster Testing',
                    'Implementation-Version': version,
                    'Main-Class': 'fi.vincit.jmobster.TestMain'
        }
    }
    dependencies {
        compile project(':core')
        compile project(':languages:javascript')
        compile project(':validators:jsr303')
        compile project(':frameworks:backbone:backbone-core')
        compile project(':frameworks:backbone:backbone-jsr303')
    }
}
